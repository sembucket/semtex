#!/bin/csh -f
#
# Try to figure out what kind of machine/OS this is
#
# $Id$
# 
foreach dir (/bin /usr/bin /usr/local/bin)
	if (-x $dir/uname ) then
	    set UNAME = $dir/uname
	    break
	endif
end
set ARCHLIST = (`$UNAME -s` `$UNAME -m`)
foreach dir ( /bin /usr/bin /usr/local/bin )
    if (-x $dir/arch) then
	set ARCHLIST = ($ARCHLIST `$dir/arch`)
    endif
end
foreach arch ($ARCHLIST)
    set arch = `echo $arch | sed 's/ //g'`
    switch ($arch)
        case Linux:
            set ARCH = Linux
            breaksw
	case IRIX:
	case IRIX64:
	    set ARCH = $arch
	    breaksw
	case cray:
	case CRAY*:
	    set ARCH = CRAY
	    breaksw
	case sun4*:
	    set version = `$UNAME -r | awk '{FS="."; print $1}'`
	    if ($version >= 5) then
		set ARCH = solaris
	    else
		set ARCH = sun4
	    endif
	    breaksw
        case HP-UX:
            set ARCH = HP-UX
            breaksw
	default:
	    breaksw
    endsw
end
echo $ARCH
exit 0


