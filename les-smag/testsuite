#!/bin/csh
##############################################################################
# Run Navier--Stokes solver on test files.
# $Id$

echo "******************************************************"
echo "* Spectral element Navier--Stokes solver test suite. *"
echo "*   This is for DNS version of LES solver, must be   *"
echo "*              compiled with NOMODEL=1.              *"
echo "*                                                    *"
echo "* (NB: pressure error norms are usually irrelevant.) *"
echo "******************************************************"

set CODE=les-smag
set MESHDIR=../semtex/mesh

if (!(-e $CODE)) then
	echo "Building application, please wait..."
	if { gmake NOMODEL=1 } then
	    gmake clean
	    echo "Done."
	else
	    echo "Unable to compile."
	    exit 1
	endif
	echo ""
endif

echo ""
echo ""
echo "-- CARTESIAN GEOMETRY --"
echo ""
echo ""
echo "*** 2D TESTS ***"

foreach SESS (taylor2 kovas1)
  if (!(-e $SESS)) cp $MESHDIR/$SESS .
  if (-e $SESS.num) rm -f $SESS.num
  echo ""
  head -1 $SESS
  enumerate -O3 $SESS > $SESS.num
  compare $SESS > $SESS.rst
  $CODE -chk $SESS > /dev/null
  compare $SESS $SESS.fld > /dev/null
  rm $SESS*
end

echo ""
echo ""
echo "*** 3D TESTS ***"

foreach SESS (taylor3 taylor4 taylor5 kovas2 kovas3 kovas4 kovas5)
  if (!(-e $SESS)) cp $MESHDIR/$SESS .
  if (-e $SESS.num) rm -f $SESS.num
  echo ""
  head -1 $SESS
  enumerate -O3 $SESS > $SESS.num
  compare $SESS > $SESS.rst
  $CODE -chk $SESS > /dev/null
  compare $SESS $SESS.fld > /dev/null
  rm $SESS*
end

echo ""
echo ""
echo "-- CYLINDRICAL GEOMETRY --"

foreach SESS (tube1 tube2 tube3 sbr tc1 cylkov2)
  if (!(-e $SESS)) cp $MESHDIR/$SESS .
  if (-e $SESS.num) rm -f $SESS.num
  echo ""
  head -1 $SESS
  enumerate -O3 $SESS > $SESS.num
  compare $SESS > $SESS.rst
  $CODE -chk $SESS > /dev/null
  compare $SESS $SESS.fld > /dev/null
  rm $SESS*
end

# Note: tc1 gives large errors, but these seem like a CFL instability
# as reducing the timestep gets rid of them: (200 x 0.001)
#
# Field 'u': norm_inf: 2.2474e-04
# Field 'v': norm_inf: 3.7103e-05
# Field 'w': norm_inf: 3.3585e-06
# Field 'p': norm_inf: 6.6341e-02
#
# Likewise with cylkov2, reducing the timesteps (going 1000 x 0.001)
# changes the errors to
#
# Field 'u': norm_inf: 9.6606e-03
# Field 'v': norm_inf: 8.3193e-03
# Field 'w': norm_inf: 7.6242e-03
# Field 'p': norm_inf: 4.7013e-01

