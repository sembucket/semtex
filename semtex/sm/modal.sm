moden 1		# Plot Modal energies
		DATA $1
		READ { t 1 m 2 e 3 }
		SET le = lg(e)
		LIMITS t le
		NOTATION -5 5 1 1
		TICKSIZE 0 0 -1 0
		LOCATION 5000 31000 5000 31000
		EXPAND 1.1
		BOX
		POINTS t le
		XLABEL \it t
		YLABEL \it E_k
		EXPAND 1.0
mavg  123	# Compute average modal energies
		# usage: mavg nmodes ek specvec
		# input:  nmodes  number of Fourier modes
		#         ek      a vector of data with modal energies
		# output: specvec a vector, nmodes long, with average energies
		DEFINE _nmode $1
		DEFINE _ek    $2
		DEFINE _spec  $3
		DEFINE _ndata (dimen ($_ek))
		DEFINE _navg  ($_ndata / $_nmode)
		SET DIMEN($_spec) = $_nmode
		DO i=0,($_navg - 1) {
		  DO j=0,($_nmode - 1) {
		  SET $_spec[$j] = $_spec[$j] + $_ek[($i * $_nmode + $j)]
		  }
		}
		SET $_spec  = $_spec / $_navg
modav 123	# Average & plot modal energies
		# usage: modav filename nmodes line
		DATA $1
		READ { ek 3 }
		mavg $2 ek spec
		SET ki = 0, ($_nmode - 1), 1
		SET _lavg = LG(spec)
		SET ki    = ki + 0.000001
		NOTATION -5 5 1 1
		TICKSIZE 0 0 -1 0
		EXPAND 1.4
		PTYPE 20 3
		LOCATION 5000 31000 5000 31000
		LIMITS ki _lavg
		BOX
		XLABEL \i k
		YLABEL \i E(k)
		IF( $?3 ) { SET _mode = $3 } ELSE { SET _mode = 0 }
		IF( _mode == 1 ) {
		  CONNECT ki _lavg
		  POINTS  ki _lavg
                } else {
		  POINTS  ki _lavg
		}

	